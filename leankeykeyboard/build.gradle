apply plugin: 'com.android.application'

android {
    // Latest gradle fix: https://stackoverflow.com/questions/76300671/android-getting-error-namespace-not-specified
    namespace 'com.slideos.system'

    // Latest gradle fix: https://stackoverflow.com/questions/22604627/gradle-buildconfigfield-buildconfig-cannot-resolve-symbol
    buildFeatures {
        buildConfig = true
    }

    // FIX: Default interface methods are only supported starting with Android N (--min-api 24)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    compileSdkVersion project.properties.compileSdkVersion
    buildToolsVersion project.properties.buildToolsVersion

    defaultConfig {
        applicationId "com.slideos.system"
        minSdk 17
        targetSdk 17
        versionCode 81
        versionName "0.8.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            // https://medium.com/@angelhiadefiesta/how-to-obfuscate-in-android-with-proguard-acab47701577
            minifyEnabled true // enable obfuscation
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "slideOSSystem"
            def separator = "_"
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def flavor = variant.flavorName
            def apkName = project + separator + "v" + versionName + separator + flavor + separator + buildType + ".apk"
            output.outputFileName = apkName
        }
    }

    lintOptions {
        abortOnError true
        disable 'MissingTranslation'
        disable 'NewApi'
    }

    // gradle 4.6 migration: disable dimensions mechanism
    // more: https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    flavorDimensions "default"

    productFlavors {
        origin {
            applicationId "com.slideos.system"
            versionName "0.8.1"
        }
        playstore {
            applicationId "com.slideos.system"
            versionName "6.1.32"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation(project.properties.espressoCoreVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation project.properties.junitVersion
    implementation project.properties.appCompatXVersion
    implementation project.properties.leanbackCompatXVersion
    implementation project.properties.constraintXVersion
    implementation project.properties.designXVersion
    implementation project.properties.voiceOverlayVersion
}
